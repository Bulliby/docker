# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=php
pkgname=('php')

pkgver=8.1.7
pkgrel=2
arch=('x86_64')
license=('PHP')
url='https://www.php.net/'
source=("https://php.net/distributions/${pkgbase}-${pkgver}.tar.xz"{,.asc} 'apache.patch' 'apache.conf')

prepare() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	cp "../../customisation/php.ini.patch" "${srcdir}"
	cp "../../customisation/php.ini" "php.ini-production"

	patch -p0 -i "${srcdir}/apache.patch"
	patch -p0 -i "${srcdir}/php.ini.patch"
	autoconf
}

build() {
	local _phpconfig="--srcdir=../${pkgbase}-${pkgver} \
		--config-cache \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/php \
		--localstatedir=/var \
		--with-layout=GNU \
		--with-config-file-path=/etc/php \
		--with-config-file-scan-dir=/etc/php/conf.d \
		--disable-rpath \
		--mandir=/usr/share/man \
		--disable-gcc-global-regs \
        "

    local _phpextensions="\
        --with-iconv=shared \
        --with-openssl \
        --with-pdo-mysql=shared,mysqlnd \
        --enable-mbstring \
        --enable-intl=shared \
        "

	EXTENSION_DIR=/usr/lib/php/modules
	export EXTENSION_DIR

	mkdir "${srcdir}/build"
	cd "${srcdir}/build"
	ln -s "../${pkgbase}-${pkgver}/configure"
	./configure ${_phpconfig} \
                ${_phpextensions}
	make

	# apache
	# reuse the previous run; this will save us a lot of time
	cp -a "${srcdir}/build" "${srcdir}/build-apache"
	cd "${srcdir}/build-apache"
	./configure ${_phpconfig} \
		--with-apxs2 \
        ${_phpextensions}
	make
}

package_php() {
	pkgdesc='A general-purpose scripting language that is especially suited to web development'

	cd "${srcdir}/build"
	make -j4 INSTALL_ROOT="${pkgdir}" install-{modules,cli,build,headers,programs,pharcmd}
	install -D -m644 "${srcdir}/${pkgbase}-${pkgver}/php.ini-production" "${pkgdir}/etc/php/php.ini"
	install -d -m755 "${pkgdir}/etc/php/conf.d/"
    install -D -m755 "${srcdir}/build-apache/libs/libphp.so" "${pkgdir}/usr/lib/httpd/modules/libphp.so"
    install -D -m644 "${srcdir}/apache.conf" "${pkgdir}/etc/httpd/conf/extra/php_module.conf"
    install -D -m755 "${srcdir}/build/modules/intl.so" "${pkgdir}/usr/lib/php/modules/intl.so"

    cleanup_my_mess
}

cleanup_my_mess()
{
    make clean
	# remove static modules
	rm -f "${pkgdir}/usr/lib/php/modules/"*.a
	# remove empty directory
	rmdir "${pkgdir}/usr/include/php/include"
}
